// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        activity_version = "1.2.0-beta01"
        agp_version = "4.2.0-alpha13"
        appcompat_version = "1.3.0-alpha02"
        browser_version = "1.3.0-alpha06"
        compose_version = "1.0.0-alpha04"
        core_version = "1.5.0-alpha04"
        coroutines_version = "1.3.9"
        customtabs_version = "3.0.2"
        dagger_version = "2.29.1"
        desugar_version = "1.0.10"
        hilt_androidx_version = "1.0.0-alpha02"
        hilt_dagger_version = "2.29.1-alpha"
        junit_version = "4.13"
        kotlin_version = "1.4.10"
        lifecycle_version = "2.3.0-beta01"
        material_version = "1.3.0-alpha03"
        moshi_version = "1.9.3"
        okhttp_version = "3.14.9"
        retrofit_version = "2.9.0"
        room_version = "2.3.0-alpha02"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_dagger_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    repositories {
        google()
        jcenter()
    }
    if (name == "app") {
        apply plugin: "com.android.application"
    } else {
        apply plugin: "com.android.library"
    }
    apply plugin: "kotlin-android"
    android {
        compileSdkVersion 30

        defaultConfig {
            minSdkVersion 23
            targetSdkVersion 30
            versionCode 1
            versionName "1.0"
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            force "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
            force "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks {
    wrapper {
        gradleVersion = "6.7-rc-3"
        distributionType = Wrapper.DistributionType.ALL
    }
}
